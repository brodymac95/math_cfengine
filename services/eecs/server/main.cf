##############################################################################
#
#   server/main.cf - EECS server configuration
#   Maintained: Ben Roose, ver 0.1
#
###############################################################################



# PROTIP: Use body file control to help make modular policy.
# "There can be only one" body common control per policy entry (promises.cf,update.cf)

# TESTING ONLY:INPUT STANDARD LIBRARY (Remove after testing)
body file control
{
      inputs => { "$(sys.libdir)/stdlib.cf" };
}

######################################################################
# Public entry point: none
######################################################################

# NOTES for future development of configuration:
# 1.


bundle server host_key_access
# @description: share host key specific directory only to that host
{

  vars:
      "key_dist"
      string => "$(sys.workdir)/host_by_key/$(connection.key)/",
      comment => "This is where agents can access host specific files.";

      
  access:

      # It's important that this path have $(connection.key) in it!
      "$(key_dist)"
      admit_keys =>  { $(connection.key) },
                    comment => "Grant each host to access its own copy of the ldap client files.
                                We assume that each host has a unique keypair";

  reports:
    (inform_mode|verbose_mode)::
      "$(this.bundle): Activated";

    (DEBUG|DEBUG_access)::

}

######################################################################

bundle server srv_paths_access
# @description: share specific directories in /srv/cfengine on policy hub to all hosts
{

  vars:

    policy_server::
      "path[software_packages]"
        string => "/srv/cfengine/packages",
        comment => "Define software repository source location";

      "path[system_configs]"
        string => "/srv/cfengine/configs",
        comment => "Define system and user configuration repository source location";

      "path[user_lists]"
        string => "/srv/user_lists",
        comment => "Define user lists repository source location used by ITS for uploading database query files";

      "paths"
        slist => getvalues(path);
      
  access:

    any::
      "$(paths)"
      handle => "server_access_grant_locations_to_hosts",
      admit => { ".*$(def.domain)", @(def.acl) },
      comment => "Grant access to directory paths for all hosts";

  reports:
    (inform_mode|verbose_mode)::
      "$(this.bundle): Activated";

    (DEBUG|DEBUG_access)::

}

######################################################################

bundle agent config_policy_server
# @description: ensure custom elements for policy hub are running
{
  vars:
      "dependent_packages" slist => { "git", "binutils", },
        comment => "set of packages required for policy server (cf-pwhash.sh needs strings command in binutils)";

      "cf_pwhash_file" string => "/usr/local/sbin/cf-pwhash.sh";
      "cf_pwhash_template" string => "$(this.promise_dirname)/data/cf-pwhash.sh.mustache";

      "master_root_passwd_location" string => "/root/master_root_passwd";

      "host_by_key_dir" string => "/var/cfengine/host_by_key/";

      "user_lists_dir" string => "/srv/user_lists";
      "its_scp_user" string => "appworx";
      "its_scp_user_desc" string => "ITS user for student list scp";

  files:
    policy_server::
      "$(host_by_key_dir)/."
	create => "true",
        comment => "ensure host_by_key directory exists";

      "$(master_root_passwd_location)"
        perms => mog("400","root", "root"),
        comment => "ensure restricted permissions on root password file";

      "$(cf_pwhash_file)"
        create => "true",
        perms => mog("700","root", "root"),
        template_method => "mustache",
        edit_template => "$(cf_pwhash_template)",
      # action => if_elapsed("60"),
        classes => results("bundle", "cf_pwhash_file"),
        comment => "expand pwhash script to /usr/local/bin using template in this promise directory";
      
      "$(user_lists_dir)/."
	create => "true",
        perms => mog("770","root", "$(its_scp_user)"),
        comment => "ensure user_lists directory exists and has correct permissions for ITS access";

  users:
    policy_server::
      "$(its_scp_user)"
      policy => "present",
      description => "$(its_scp_user)",
      shell => "/bin/bash",
      home_dir => "/home/$(its_scp_user)",
      home_bundle => home_skel( "$(its_scp_user)","$(its_scp_user)", "$(user_settings.default_mode)", "/home/$(its_scp_user)", "$(user_settings.home_skeleton)" ),
      comment => "add local ITS appworx user to policy server for nightly scp of student enrolment lists";
      
  methods:
    install_software::
      "install dependent packages"
    	usebundle => package_latest("$(dependent_packages)"),
        classes => results("bundle", "dependent_packages_install");      

    policy_server::
      "ensure ssh authorized_keys is copied into ITS appworx user (use bundle in manage/ssh/main.cf)"
        usebundle => ensure_user_ssh_authorized_keys( "$(its_scp_user)", "$(its_scp_user)", "/home/$(its_scp_user)" ),
        classes => results("bundle", "$(its_scp_user)_ssh_auth_keys");

  reports:
    (inform_mode|verbose_mode)::
      "$(this.bundle): Activated";

    (DEBUG|DEBUG_config)::
      "=== $(this.bundle) ===";
}

######################################################################
