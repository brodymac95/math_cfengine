###############################################################################
#
#   manage/login_defaults/main.cf - EECS login banner and defaults configuration
#   Maintained: Ben Roose, ver 0.2
#
###############################################################################

body file control
{
      inputs => { "$(sys.libdir)/stdlib.cf" };
}

bundle agent config_login_banner
# @description: sets the /etc/motd login banner from a template file
{
  vars:
      "template_file" string => "$(this.promise_dirname)/motd_template.txt";
      "motd_file"     string => "/etc/motd";

    ## NOTE: support var has been moved to global bundle in clasification/main.cf

    ## REMOVE ONCE FULLY CHANGED TO ROLE
    # os_tenant::
    #   "type" string => "OpenStack Tenant";
    # os_node::
    #   "type" string => "OpenStack Node";
    # server::
    #   "type" string => "Server";
    # workstation::
    #   "type" string => "Workstation";

  files:
    "$(motd_file)"
      handle => "set_login_banner",
      comment => "Ensure the login banner is set to the authorized text",
      create => "true",
      perms => mog ("644", "root", "root"),
      edit_defaults => empty,
      edit_line => expand_template("$(template_file)");

  reports:
    (inform_mode|verbose_mode)::
      "$(this.bundle): Activated";

    (DEBUG|DEBUG_config_login_banner)::
}

###############################################################################

bundle agent config_login_defs
# @description: sets default umask for all users. May be expanded to change other login defaults
{
  vars:
      # files to edit
    debian::
      # Debian/Ubuntu set umask in /etc/login.defs if pam_umask.so is enabled
      "files[login_defs]"   string => "/etc/login.defs";
      "files[common_session]"   string => "/etc/pam.d/common-session";

      "pam_umask_enable" string => "session optional        pam_umask.so";

    redhat::
      # RHEL 6 and 7 seem to set umask in /etc/bashrc
      # Need to add RHEL configuration to this bundle when it's wanted!
      # see https://linuxaria.com/article/linux-shell-understanding-umask-with-examples
      
    any::
      # login.defs configuration to set
      "login_defs[UMASK]"    string => "0027",
	policy => "free",
        ifvarclass => not( isvariable("user_settings.custom_umask") );

      "login_defs[UMASK]" string => "$(user_settings.custom_umask)",
	policy => "free",
	ifvarclass => and( isvariable("user_settings.custom_umask") ),
	comment => "set the default umask setting to 027 unless custom_umask in
                    manage/user/main.cf user_settings bundle is set";

  methods:
    debian::
      "edit login.defs"    usebundle => edit_login_defs;

  reports:
    (inform_mode|verbose_mode)::
      "$(this.bundle): Activated";

    (DEBUG|DEBUG_config_login_defs)::
      "=== $(this.bundle) ===";
    (DEBUG|DEBUG_config_login_defs).debian::
      "default umask value: $(login_defs[UMASK])";
    (DEBUG|DEBUG_config_login_defs).redhat::
      "WARNING: $(this.bundle) has not been configured for Red Hat OS";

}

###############################################################################

bundle agent edit_login_defs
{
  files:
      "$(config_login_defs.files[login_defs])"
      handle => "edit_login_defs",
      edit_line => set_line_based("config_login_defs.login_defs", " ", "\s+", ".*", "\s*#\s*"),
      comment => "Set desired login.defs parameters";
      #classes => results("bundle", "login_defs");

      "$(config_login_defs.files[common_session])"
      handle => "edit_pam_common_session",
      edit_line => append_if_no_line("$(config_login_defs.pam_umask_enable)"),
      comment => "Set pam to allow for /etc/login.defs to be activated";
}
